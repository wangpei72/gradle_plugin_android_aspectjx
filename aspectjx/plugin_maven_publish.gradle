apply plugin: 'maven-publish'
apply plugin: 'signing'

// 设置group和版本名称
group = LIB_GROUP
version = LIB_VERSION

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        // mavenJava：自定义发布产物名称
        mavenJava(MavenPublication) {
            artifactId = LIB_ARTIFACT
            from components.java

            pom {
                name = LIB_ARTIFACT
                description = LIB_DES
                url = LIB_URL
                licenses {
                    license {
                        name = LICENSE_NAME
                        url = LICENSE_URL
                        distribution = LICENSE_DIST
                    }
                }
                developers {
                    developer {
                        id = DEVELOPER_ID
                        name = DEVELOPER_NAME
                        email = DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection = SCM_CONNECTION
                    developerConnection = SCM_DEV_CONNECTION
                    url = SCM_URL
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            def mavenUrl = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            url = mavenUrl

            println("publish to mavenUrl=$mavenUrl")
            // 校验不支持本地文件目录
            if (mavenUrl.startsWith("https")) {
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}

signing {
    // 指定要签名的产物
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}